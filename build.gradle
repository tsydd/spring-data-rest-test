version = '1.0'

task wrapper(type: Wrapper) {
  gradleVersion = '2.0'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.1.4.RELEASE'
    }
}

project(':data') {
    apply plugin: 'java'
    apply plugin: 'spring-boot'
    apply plugin: 'war'

    sourceCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
    }

    dependencies {
        versionManagement 'io.spring.platform:platform-versions:1.0.1.RELEASE@properties'

        compile 'org.springframework.data:spring-data-rest-webmvc'
        compile 'org.springframework.data:spring-data-jpa'
        compile 'org.hibernate:hibernate-entitymanager'
        compile 'org.hsqldb:hsqldb'
        compile 'org.slf4j:slf4j-log4j12'

        providedCompile 'javax.servlet:javax.servlet-api'

        testCompile 'org.testng:testng'
        testCompile 'org.springframework:spring-test'
        testCompile 'org.assertj:assertj-core:1.6.1'
    }

    bootRepackage {
        enabled = false
    }

    configurations.all {
        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                if (details.requested.group == 'org.springframework') {
                    details.useVersion '4.0.6.RELEASE'
                }
            }
        }
    }
}